plugins {
    id 'java-library'
    id "org.sonarqube" version "3.3"
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
}

group 'io.mishustin'
version '0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.testng', name: 'testng', version: '7.4.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
    implementation group: 'org.json', name: 'json', version: '20211205'
}

test {
    finalizedBy jacocoTestReport
    useTestNG();
}

java{
    withJavadocJar()
    withSourcesJar()
}

sonarqube {
    properties {
        property "sonar.projectKey", "mishust1n_testrail-testng-client"
        property "sonar.organization", "mishust1n"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

signing {
    sign(publishing.publications)
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {

                description = 'Testrail client for the testNg'
                name = 'Testrail-TestNg-client'
                url = 'https://github.com/mishust1n/qa-toolbox'
                organization {
                    name = 'io.mishustin'
                    url = 'https://github.com/mishust1n'
                }

                licenses {
                    license {
                        name = 'GNU General Public License'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                scm {
                    connection = 'scm:https://github.com/mishust1n/testrail-testng-client.git'
                    developerConnection = 'scm:git:https://github.com/mishust1n/testrail-testng-client.git'
                    url = 'scm:git:https://github.com/mishust1n/testrail-testng-client.git'
                }

                developers {
                    developer {
                        id = 'mishust1n'
                        name = 'Artur Mishustin'
                        email = 'artur@mishustin.io'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}